/* Rexx Swid */
/* .................................................................. */
/* Author  : Kannan AK                                                */
/* Swid    : Switch ID and Job Character                              */
/* Notice  : None                                                     */
/* Version : Alpha                                                    */
/* .................................................................. */
address ispexec
"ISREDIT MACRO NOPROCESS"
if RC = 20 then do
    zedsmsg = "Not a TSO Command"
   "setmsg msg(isrz000)";exit
end
alpha = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890'
"VGET JPOINT PROFILE"
if RC > 0 then do
   JPOINT = 1
   "VPUT JPOINT PROFILE"
end
"isredit (first) = linenum .zf"
"isredit (last)  = linenum .zl"
"isredit (linebuf) = line" 1
if substr(linebuf,1,2) \= "//" then do
    zedsmsg = "Error identifying JCL"
   "setmsg msg(isrz000)";exit
end

   o_jobchar = substr(linebuf,10,1)
   jobname = substr(linebuf,3,7)
   n_jobchar  = "'"||substr(alpha,jpoint,1)||"'";jpoint=jpoint+1
   if jpoint >= 37 then jpoint = 1
   "VPUT JPOINT PROFILE"

   "isredit change p'======='" USERID() "3 9"
   "isredit change p'='" n_jobchar "10"

   do i = first to last by 1
     "isredit (linebuf) = line" i
      posnoti = pos('NOTIFY=',linebuf)
      befnoti = posnoti + 6
      aftnoti = posnoti + 7
      if posnoti > 0 then do
         s1 = substr(linebuf,1,befnoti)
         s2 = substr(linebuf,aftnoti,7)
         s3 = substr(linebuf,aftnoti+7)
         s2 = USERID()
         newline =  s1||s2||s3
         "isredit line" i " = " '(newline)'
         leave
      end
   end
exit
