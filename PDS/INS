/* REXX INS                                                           */
/* .................................................................. */
/* AUTHOR: KANNAN                                                     */
/* SYNTAX: CMD STR  COL OPT                                           */
/* USAGE : INS THIS 23  S  >> WILL SHIFT;LINE IS FULL CHARS WILL BE   */
/*                         >> LOST AT THE END OF LINE                 */
/* USAGE : INS THAT 23  O  >> OVERLAY GIVEN STRING                    */
/* .................................................................. */
ADDRESS ISPEXEC
PARSE SOURCE . . ME .
"ISREDIT MACRO (CHAR COL OPT) NOPROCESS"
IF RC = 20 THEN DO
    ZEDSMSG = "NOT A TSO COMMAND"
   "SETMSG MSG(ISRZ000)";EXIT
END
OPT=STRIP(OPT,B);OPT = TRANSLATE(OPT)
IF  (OPT \= 'S' & OPT \= 'O')  THEN DO
          ZEDSMSG = ME "NEEDS PARM S/O"
          "SETMSG MSG(ISRZ001)"
          EXIT
     END
"ISREDIT PROCESS RANGE J"
IF RC > 0 THEN DO
      ZEDSMSG = "IMPROPER LINE COMMAND"
      "SETMSG MSG(ISRZ001)"
       EXIT
END
"ISREDIT (RANG)  = RANGE_CMD"
"ISREDIT (FIRST) = LINENUM .ZFRANGE";
"ISREDIT (LAST)  = LINENUM .ZLRANGE" ;
CLEN = LENGTH(CHAR)

DO I = FIRST TO LAST BY 1
     "ISREDIT (LINEBUF) = LINE" I
     IF RANG = 'J' & OPT = 'S' THEN
        LINEBUF=INSERT(CHAR,LINEBUF,COL)
     IF RANG = 'J' & OPT = 'O' THEN
        LINEBUF=OVERLAY(CHAR,LINEBUF,COL,CLEN)
     "ISREDIT LINE" I " = " '(LINEBUF)'
END

ZEDSMSG = (LAST-FIRST+1) "LINE(S) EDITED"
"SETMSG MSG(ISRZ000)"
EXIT
