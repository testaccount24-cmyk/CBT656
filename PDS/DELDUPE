/* Rexx DELDUPE */
Address isredit
"ISREDIT MACRO (LCOL,RCOL)"  /* Indicate macro with 2 parms    */
"ISREDIT (DW) = DATA_WIDTH"  /* Tet maximum data width         */
"ISREDIT (LAST) = LINENUM .ZLAST"    /* Get number of last line*/
"ISREDIT RESET X"            /* Un-Exclude all lines           */
If datatype(lcol,'N')=0 Then lcol=1  /* Set left column        */
If datatype(rcol,'N')=0 | rcol>dw Then rcol = dw /* Right col  */
len=abs(rcol-lcol)+1         /* Get length of compare area     */
lcol=min(lcol,rcol)          /* Tet left col                   */
Do linenum = 1 to last       /* Loop through all lines         */
  "ISREDIT (LINE) = LINE "linenum /* Place line data in 'line' */
  test=substr(line,lcol,len) /* Take just columns to compare   */
  If test = testold Then     /* Compare against previous line  */
    "ISREDIT XSTATUS "linenum" = X"  /* If match, exclude line */
  testold = test             /* Save current line compare area */
End                          /*                                */
"ISREDIT DEL ALL X"       /* Finally, delete all excluded lines*/
Exit 1                       /* Reset cursor to command line   */
