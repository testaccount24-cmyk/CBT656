/* Rexx Masscopy                                                     */
/* Syntax : tso YYMM                                                 */
arg YYMM
if YYMM = '' | datatype(YYMM) <> 'NUM' then do
   ZEDSMSG = '*Need numeric YYMM*'
   "ISPEXEC SETMSG MSG(ISRZ000)"
   exit 1
end
localuser = Userid()
wjcount = 0
cn      = 0
address ISPEXEC

address ISPEXEC "Control Errors Return"
call parse_listcat
call build_jobcard
call feed_job_details
call edit_jcl

qui :
address ISPEXEC 'LMFREE DATAID('trap')'
address TSO 'FREE F('ddname')'
exit

build_jobcard:
address TSO
call wj("//"userid()"$ JOB (000000),'BT:KANNAN AK" yymm"',            ")
call wj("//         CLASS=U,MSGCLASS=X,NOTIFY=&SYSUID,COND=(5,LT)     ")
return

build_jcl :
cn = cn + 1
tx = translate(format(cn,3,0),'0',' ')
call wj("//*---------------------------------------------------------*")
call wj("//QCOPY"tx" EXEC PGM=IDCAMS                                  ")
call wj("//SYSPRINT DD SYSOUT=*                                       ")
call wj("//SYSUT1   DD DISP=SHR,DSN="fstr"                            ")
call wj("//SYSUT2   DD DSN="udsname",                                 ")
call wj("//            DISP=(NEW,CATLG,DELETE),                       ")
call wj("//            SPACE=(CYL,(100,100),RLSE),                    ")
call wj("//            RECFM=FB,LRECL=135,BLKSIZE=0                   ")
call wj("//*           DCB=(userid.model.VB)                          ")
call wj("//SYSIN    DD *                                              ")
call wj("  REPRO INFILE(SYSUT1) OUTFILE(SYSUT2)                       ")
call wj("/*                                                           ")
return

wj :
parse arg stream
queue stream
wjcount = wjcount + 1
return

edit_jcl :
ZEDSMSG = '*Verify & submit*'
"ISPEXEC SETMSG MSG(ISRZ000)"
if queued() > 0 then
do
 address ISPEXEC 'VGET (ZSCREEN) SHARED'
 ddname='$MASS$'zscreen
 address TSO
 'ALLOC F('ddname') UNIT(VIO) DSO(PS) RECFM(F B) LRECL(080) NEW DEL REU'
 'EXECIO 'queued()' DISKW 'ddname' (FINIS'
 address ISPEXEC 'LMINIT DATAID(TRAP) DDNAME('ddname') ENQ(SHR)'
 address ISPEXEC 'EDIT   DATAID('trap')'
end
return

parse_listcat :
tapedsn. = ''
k = 1
f = outtrap('out.','*','noconcat')
address TSO "LISTCAT LEVEL ('SYSAK.FILES"YYMM"') NAME NVSAM"
listrc = RC
f = outtrap(off)
if listrc \= 0 then do
   do j = 1 to out.0
      say out.j
   end
   exit listrc
end
hlq = 'SYSAK.FILES'
do i = 1 to out.0
  if pos(hlq,out.i) <> 0 then
    if pos(hlq,out.i) > 0 then do
       parse value out.i with . . dsname
       if pos('REJECT1',out.i) = 0 & ,
          pos('REJECT2',out.i) = 0 then do
             tapedsn.k = dsname
             k = k + 1
       end
    end
end
tapedsn.0 = k-1
return


feed_job_details :
do i = 1 to tapedsn.0
   fstr = tapedsn.i
   udsname = localuser || substr(tapedsn.i,8)
   call change udsname 'TAPES' 'DISK'
   udsname = target
   call build_jcl
end
return

change : procedure expose target oldstr newstr
arg target oldstr newstr
if pos(oldstr,target) = 0 then return 'ERROR'

if length(oldstr) >= length(newstr) then
target = space(overlay(newstr,target,pos(oldstr,target),length(oldstr)),0)

if length(oldstr) < length(newstr) then
target = substr(target,1,pos(oldstr,target)-1) || newstr || ,
         substr(target,pos(oldstr,target)+length(oldstr))
return target

