/* REXX  */
/********************************************************************/
/*   RUNSQL subroutine                                              */
/*                                                                  */
/*  Invocation:  TSO RUNSQL DB2SSID SYSINDSN                        */
/*  This REXX takes the DB2 SSID and the SYSIN dataset as argument  */
/*  It then runs DSNTIAUL and stores the result in a file           */
/*  named as PREFX.USERID.SYSRECXX                                  */
/*  The SYSPRINT dataset is also present and the code can be        */
/*  turned on or off to browse the same                             */
/*  Use proper values for PREFX as allowed by your installation     */
/********************************************************************/
TRACE o
PARSE UPPER ARG P_db2sub P_dsname .
say 'Running RUNSQL'
if strip(P_db2sub)='' | strip(P_dsname) = '' then
do
   say 'Proper execution is RUNSQL DB2SSID SYSIN-DSNAME  ...'
   exit(8)
end
PREFX = userid()
x = outtrap("zap.","*")
CALL P1XXX_Allocate_Sysin
CALL P2XXX_Allocate_Output
Call P3XXX_Execute_DSNTIAUL
Call P4XXX_Clean_up
x = outtrap("OFF")
EXIT

P1XXX_Allocate_Sysin:
   P_pds_in = strip(P_dsname)
   Sysin = SYSDSN("'"P_pds_in||"'")
   if Sysin = "OK" then do
     address TSO "ALLOCATE DDNAME(SYSIN) SHR reu ",
      "DSNAME('"P_pds_in||"')"
   end
   if Sysin /= "OK"  then do
     SAY  '*** Error *** 'P_pds_in Sysin
     exit(8)
   end
   address TSO "delete "USERID()".SYSPRINT"
   address TSO "delete "USERID()".SYSPUNCH"
RETURN

P2XXX_Allocate_Output:
address TSO "ALLOC F(SYSREC00) NEW UNIT(SYSDA) SPACE(1,10)",
            "UNIT(sysda)",
            "CYL    REUSE DSNAME('"USERID()".SYSREC00')"
address TSO "ALLOC F(SYSPRINT) NEW UNIT(SYSDA) SPACE(8,2)",
            "UNIT(sysda)",
            "TRACKS REUSE DSNAME('"USERID()".SYSPRINT')"
address TSO "ALLOC F(SYSPUNCH) NEW UNIT(SYSDA) SPACE(8,2)",
            "UNIT(sysda)",
            "TRACKS REUSE DSNAME('"USERID()".SYSPUNCH')"
Return

P3XXX_Execute_DSNTIAUL:
/*******************************************************************/
/*  Specify the proper DSNLOAD and DSNEXIT  Dataset library names  */
/*******************************************************************/

  Address ISPEXEC "libdef steplib dataset id(",
                  "'SYS0.DB2.PROD.DSNLOAD')"

/*                                                                 */
"NEWSTACK"
QUEUE "RUN PROGRAM(DSNTIAUL) PLAN(DSNTIAUL) ",
      "LIBRARY('SYSPROD.RUNLIB.LOAD') ",
      "PARM('SQL')"
QUEUE "END"
QUEUE
address TSO "DSN SYSTEM("P_db2sub")"
"DELSTACK"

/*  comment the signal code below to browse SYSPRINT dataset       */

/*     BROWSE THE SYSPRINT FILE                                    */
ADDRESS ISPEXEC "LMINIT DATAID(DSID) DDNAME(SYSPRINT)"
ADDRESS ISPEXEC "BROWSE DATAID("DSID")"
ADDRESS ISPEXEC "LMFREE DATAID("DSID")"
RETURN

P4XXX_Clean_up:
address TSO "delete '"USERID()".SYSPRINT'"
address TSO "delete '"USERID()".SYSPUNCH'"
RETURN
