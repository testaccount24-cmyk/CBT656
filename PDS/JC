/* Rexx JC                                                            */
/* .................................................................. */
/* Author  : Kannan AK                                                */
/* JC      : Jobcard creator                                          */
/* Notice  : None                                                     */
/* Version : Alpha                                                    */
/* .................................................................. */
Address ISREDIT
'MACRO'
IF RC = 20 THEN DO
    ZEDSMSG = "NOT A TSO COMMAND"
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ000)"
    EXIT
END
LineNo=0

If sysvar(Syspref) <> '' Then
    Prefix = sysvar(Syspref)
Else
    Prefix = userid()

Address ISPEXEC 'VGET (ZACCTNUM)'
Acct = ZACCTNUM

Call Getname
Name="'"||Name||"'"

Call Add '//'||Prefix||'A'||' JOB '||Acct||","||Name||","
Call Add '//         CLASS=U,'
Call Add '//         MSGLEVEL=(1,1),'
Call Add '//         MSGCLASS=X,'
Call add '//         NOTIFY=&SYSUID'
Call Add '//*'
Return

Add:
Parse Arg line
'LINE_AFTER' LineNo' = (LINE)'
LineNo=LineNo+1
Return

GetName:
ascbasxb=d2x(c2d(storage(224,4))+108)
asxbsenv=d2x(c2d(storage(ascbasxb,4))+200)
aceeunam=d2x(c2d(storage(asxbsenv,4))+100)
adr=c2x(storage(aceeunam,4))
Name=storage(d2x(c2d(storage(aceeunam,4))+1),c2d(storage(adr,1))-1)
Name=space(strip(Name,"b"," "),0)
Return
