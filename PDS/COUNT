/* REXX COUNT
   TSO and/or ISPF ONLY
   REXX command to count records if sequential,
       count members if a pds

   to use:
   COUNT the-dataset-to-be-counted

   IF INPUT IS SEQUENTIAL, COUNTS RECORDS
   IF INPUT IS A PDS, COUNTS MEMBERS
   NO OTHER TYPES OF INPUT WILL WORK.

   FOR TSO/ISPF ONLY

  for REXX information, Sample Code, books see

                 HTTP://WWW.REXXFILES.COM



  provided this entire comment is included

*/

ARG DSN  .
IF DSN = "" THEN SIGNAL ADIOS

SYSDSORG = ""
CALL LISTDSI DSN "DIRECTORY" "RECALL"

IF SYSDSORG = "PS" THEN DO
  NAME_TO_COUNT = TRANSLATE(DSN," ","'")
  NAME_TO_COUNT = SPACE(NAME_TO_COUNT,0)
  "ALLOC DSN(*) DDN(SYSPRINT) REUSE"
  "ALLOC DDN(INFILE) SHR REUSE DSN("DSN")"
  "ALLOC DUMMY DDN(OUTFILE) REUSE  BLKSIZE(32000)"


  CALL OUTTRAP "SYSPRINT." , "*"

  "REPRO INFILE(INFILE) OUTFILE(OUTFILE) "
  CALL OUTTRAP "OFF"

  DO I = 1 TO SYSPRINT.0
    /*SAY SYSPRINT.I*/
    PARSE VAR SYSPRINT.I "WAS" RECORD_COUNT
    IF DATATYPE(RECORD_COUNT) = "NUM" THEN LEAVE
  END I

  IF DATATYPE(SYSLRECL) = "NUM" THEN DO
     SAY "RECORD COUNT  = " RECORD_COUNT
     SAY "RECORD LENGTH = " SYSLRECL
     SAY "BYTE COUNT    = " SYSLRECL * RECORD_COUNT
     END

  "ALLOC DDN(SYSIN) DSN(*) REUSE"
  "ALLOC DDN(SYSPRINT) DSN(*) REUSE"
  "FREE DDN(INFILE OUTFILE )"

  EXIT
  END /* FOR PS */

IF SYSDSORG = "PO" THEN DO
SAY SYSMEMBERS "MEMBERS IN PDS"
EXIT
END

IF SYSDSORG = "VS" THEN DO
SAY "CANNOT COUNT RECORDS IN VSAM DATASET"
EXIT
END

ADIOS:
SAY "COUNT WILL COUNT RECORDS IN A SEQUENTIAL FILE"
SAY "OR MEMBERS IN A PDS"
SAY "ANYTHING ELSE, CAN'T DO"
SAY "REEXECUTE. PUT COUNT TO LEFT OF DSNAME ON ISPF 3.4 DSLIST"
EXIT

/***
   may be freely distributed
    provided that this notice is attached.
    See WWW.REXXFILES.COM and
    http://www.touniteamerica.com/programming/index.html
    I am an independant consultant and trainer.
    For information write to gabe1@touniteamerica.com
***/
