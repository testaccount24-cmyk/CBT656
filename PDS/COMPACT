/* Rexx                                                               */
/* .................................................................. */
/* Author  : Kannan AK                                                */
/* COMPACT : tso compact Ýdsnlvl¨ Ý.¨                                 */
/* Notice  : Migrated datasets are excluded                           */
/*         : space or . will default your userid as dsn level         */
/*         : Does not augment sysextents                              */
/* Version : Alpha                                                    */
/* .................................................................. */
trace off
arg hlq
if  hlq = '?' then signal assist
if  hlq = ''  then hlq = userid()
if  hlq = '.' then hlq = userid()
a = ')|/_\|!0o..o0!|\_/|('
f = 1
call tempdsn co tempdd tempdsn
if result = 'ERROR' then exit
address ispexec
CONTROL ERRORS RETURN
ZWINTTL=' Defrag Diskfiles '
"vput (ZWINTTL)"
"CONTROL DISPLAY LOCK"
"ADDPOP COLUMN(6) ROW(3)"
"DISPLAY PANEL(FRAGPNL)"
indx = 1
compact. = ''
queue "*** Disk Files Compressed in the Session ***"
X = OUTTRAP('OUT.','*','NOCONCAT')
address tso "LISTC LVL('"hlq"')"
X = OUTTRAP(OFF)
Do I=1 by 1 while I <= OUT.TRAPPED
  if pos('GDG BASE',out.i) > 0  then nop
  else do
       parse value out.i with . . dsname .
       x = listdsi("'"dsname"'" directory norecall)
       if sysdsorg = 'PO' & (sysmembers > 0) then do
          pdsout  = dsname
          statout = 'Being compressed....'
          fun = substr(a,f,1);ZEDSMSG = fun;"SETMSG MSG(ISRZ000)";f=f+1;
          if f = 18 then f =1
          "CONTROL DISPLAY LOCK"
          "DISPLAY PANEL(FRAGPNL)"
          call defragmentor
          queue dsname
          statout = 'Compressed!' 'Return code =' RC
          fun = substr(a,f,1);ZEDSMSG = fun;"SETMSG MSG(ISRZ000)";f=f+1;
          if f = 18 then f =1
          "CONTROL DISPLAY LOCK"
          "DISPLAY PANEL(FRAGPNL)"
       end
  end
End
qui :
drop compact.
"REMPOP"
queue "//eol"
queue ""
address tso "EXECIO * DISKW" tempdd "(FINIS"
"ISPEXEC EDIT DATASET("tempdsn")"
address tso
IF LENGTH(tempdd) > 7 THEN "FREE DD("tempdd") DELETE"
Exit 0

defragmentor : procedure expose dsname
  FILEN ="'"||dsname||"'"
  address tso
  "ALLOC F(INPUT) DS("FILEN") REUSE OLD"
  address ispexec
  'LMINIT  DATAID('DATAID') DDNAME(INPUT) ENQ(EXCL)'
  'LMCOMP  DATAID('DATAID')'
  'LMFREE  DATAID('DATAID')'
  return

assist :
address ispexec
ZEDLMSG = 'Format : "tso compact _highlevelqual_"' || ' . to default'
"SETMSG MSG(ISRZ000)"
Exit 0

TEMPDSN : PROCEDURE EXPOSE CUECHAR TEMPDD TEMPDSN
ARG CUECHAR
CUECHAR = SUBSTR(CUECHAR,1,2)
D = DATE(J);P = SYSVAR(SYSPREF);
R1 = RANDOM(100,999);R2 = RANDOM(100,999);
T = TIME();PARSE VALUE T WITH HH ':' MM ':' SS;TT=HH||MM||SS
TEMPDSN = CUECHAR||D||'.T'||R1||TRUNC(R1*R2/RANDOM()/10)||'.Z'||TT
NT = TEMPDSN
TEMPDD  = CUECHAR ||R2||R1
"ALLOC DS("NT") DD("TEMPDD") DSORG(PS) LRECL(80) RECFM(F) NEW KEEP"
IF RC \= 0 THEN DO
       "ISPEXEC SETMSG MSG(ISRZ002)"
       RESULT = 'ERROR'
       EXIT
END
RETURN
