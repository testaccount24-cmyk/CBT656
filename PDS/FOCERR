/* Rexx                                                               */
/* Function : Return FOCUS Error for Message Number                   */
/* A bit of my tryst with FOCUS                                       */
Address ISPEXEC
Arg MsgNum
Parse Value MsgNum With m1 m2 m3 m4 m5 .
If Length(MsgNum) = 0 Then Do
   ZEDLMSG = "Provide a message number to query"
   "SETMSG MSG(ISRZ001)"
   Return
End
If (Datatype(m1) <> 'NUM' & Length(m1) <> 0) |,
   (Datatype(m2) <> 'NUM' & Length(m2) <> 0) |,
   (Datatype(m3) <> 'NUM' & Length(m3) <> 0) |,
   (Datatype(m4) <> 'NUM' & Length(m4) <> 0) |,
   (Datatype(m5) <> 'NUM' & Length(m5) <> 0) Then Do
   ZEDLMSG = "FOCUS error message number should be numeric"
   "SETMSG MSG(ISRZ001)"
   Return
End
'Control Errors Return'
Address TSO
/* System Files */
"Alloc DD(ERRORS  ) DS('SYS1.FOCUS.PROD.ERRORS.DATA'          ) SHR REU"
"Alloc DD(FOCEXEC ) DS('SYS1.FOCUS.PROD.FOCEXEC.DATA'         ) SHR REU"
"Alloc DD(FOCCTL  ) DS('SYS1.FOCUS.PROD.FOCCTL.DATA'          ) SHR REU"
/* Work Files */
"Alloc DD(FOCSTACK) UNIT(VIO) NEW DEL REU"
"Alloc DD(HOLDMAST) UNIT(VIO) NEW DEL REU"
"Alloc DD(FOCSORT ) UNIT(VIO) NEW DEL REU"
"Alloc DD(HOLD    ) UNIT(VIO) NEW DEL REU"
"Alloc DD(QQREPQQ) REU NEW DEL UNIT(SYSDA) LRECL(80) RECFM(F B)" ,
      "SPACE(10,10) TRACK RELEASE DSO(PS) BLKSIZE(0)"
"Alloc DD(SYSIN) REU NEW DEL UNIT(SYSDA) LRECL(80) RECFM(F B)" ,
      "SPACE(10,10) TRACK RELEASE DSO(PS) BLKSIZE(0)"
Call GetTempFile
/* Write the query */
Do Queued();Pull;End
Queue '-TYPE  ----------------------------------------'
Queue '-TYPE  QUERY FOCUS ERROR AND DISPLAY ERROR TEXT'
Queue '-TYPE  ----------------------------------------'
If Length(m1) <> 0 Then Queue '? 'm1
If Length(m2) <> 0 Then Queue '? 'm2
If Length(m3) <> 0 Then Queue '? 'm3
If Length(m4) <> 0 Then Queue '? 'm4
If Length(m5) <> 0 Then Queue '? 'm5
"EXECIO" Queued() "DISKW QQREPQQ (FINIS"
/* Tell FOCUS to run it */
Do Queued();Pull;End
Queue 'EX QQREPQQ'
Queue 'FIN'
"EXECIO" Queued() "DISKW SYSIN (FINIS"
Address TSO "CALL 'SYS6.FOCUS.PROD.FOCLIB.LOAD(FOCUS)'"
Do Queued();Pull;End
Address ISPEXEC 'LMINIT DATAID(TRAP) DDNAME('ddname') ENQ(SHR)'
Address ISPEXEC 'BROWSE DATAID('TRAP')'
Address ISPEXEC 'LMFREE DATAID('TRAP')'

"Free DD(ERRORS  )"
"Free DD(FOCEXEC )"
"Free DD(FOCCTL  )"
"Free DD(FOCSTACK)"
"Free DD(HOLDMAST)"
"Free DD(FOCSORT )"
"Free DD(HOLD    )"
"Free DD(QQREPQQ )"
X = OUTTRAP('juNk','*','NOCONCAT')
Address TSO "Delete "NT
X = OUTTRAP(OFF)
Return

GetTempFile :
ddname = 'SYSPRINT'
D = Date(j);R1 = Random(100,999);R2 = Random(100,999);
T = Time();Parse Value T with hh ':' mm ':' ss;TT=hh||mm||ss
NT = 'FOC'D||'.T'||R1||Trunc(R1*R2/Random()/10)||'.Z'||TT
Address TSO
"Alloc DS("NT") DD("ddname") Dsorg(PS) Lrecl(80) Recfm(F B)" ,
"New Reu Unit(VIO)"
If RC \= 0 Then Do
       "ISPEXEC SetMsg Msg(ISRZ002)"
       Exit
End
Return
