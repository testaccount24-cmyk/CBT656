/* Rexx SiteCMDS                                                      */
/* .................................................................. */
/* Author  : Kannan AK                                                */
/* SiteCMDS: Add User Commands to in-storage ISPF Command Table       */
/* Notice  : Not Sitewide, effects session wide                       */
/*         : Just named so. You can do site commands in ISPCCONF      */
/*         : QUIET option will suppress message                       */
/* Refer   : http://www.naspa.com/PDF/97/T9712009.pdf                 */
/*         : http://www.naspa.com/PDF/99/T9906008.pdf                 */
/* Version : Alpha                                                    */
/* .................................................................. */
Arg opt
Parse Source System . CmdName .
Address Ispexec
added     = 0
updated   = 0
deleted   = 0
exec_line = 70
Call GetInstreamCommands
 Do Queued()
   Pull  c
   Upper c
   If word(c,1) = "/*" Then Iterate
   If c = ""           Then Iterate
   dlm=substr(c,1,1)
   Parse Var c (dlm) verb (dlm) trunc,
               (dlm) act  (dlm) desc (dlm) .
   Upper verb act
   If dlm = '-' Then Call TableDelete
   Else Call TableUpdate
 End
   If wordpos('QUIET',opt) = 0 Then
   zedlmsg = CmdName || ,
             " : Added("added") Updated("updated") Deleted("deleted")"
   Address Ispexec "setmsg msg(isrz001)"
Exit 0

TableUpdate:
 If LocateCommand(verb) = 0 Then Do
   Call UpdateVar
   'tbput ISPCMDS'
   updated = updated + 1
   Return
 End
 'tbtop ISPCMDS'
 Call UpdateVar
 'tbadd ISPCMDS'
 added = added + 1
 Return

TableDelete:
 If LocateCommand(verb) > 0 Then return
 'tbdelete ISPCMDS'
 deleted = deleted + 1
 Return

UpdateVar:
 zctverb=verb
 zcttrunc=trunc
 zctact=act
 zctdesc=desc
 Return

LocateCommand:
 "tbtop    ISPCMDS"
 Arg zctverb
 "tbscan   ISPCMDS arglist(zctverb)"
 Return RC

GetInstreamCommands:
Do queued();Pull;End
Do Until substr(line,1,9)='/*COMMAND'
  line = sourceline(exec_line)
  exec_line = exec_line + 1
End
Do until substr(line,1,2)='*/'
  line = sourceline(exec_line)
  If substr(line,1,2) \= '*/' Then
    Queue line
    exec_line = exec_line + 1
End
Return

/*COMMAND
/SX/0/ALIAS SAVE/SHORT FORM OF SAVE/
/PREV/0/ALIAS RETRIEVE/SHORT FORM OF RETRIEVE/
/PDE/0/SELECT PANEL(PDS@PRIM)/STARTOOL OR PDS 8.5 MAIN ENTRY PANEL/
/SP0/0/SELECT PGM(ISPISM) SCRNAME(SETTINGS)/ISPF 0/
/SP1/0/SELECT PGM(ISRBRO) PARM(ISRBRO01) SCRNAME(VIEW)/ISPF 1/
/SP2/0/SELECT PGM(ISREDIT) PARM(P,ISREDM01) SCRNAME(EDIT)/ISPF 2/
/SP3/0/SELECT PANEL(ISRUTIL) SCRNAME(UTIL)/ISPF 3/
/SP4/0/SELECT PANEL(ISRFPA) SCRNAME(FOREGRND)/ISPF 4/
/SP5/0/SELECT PGM(ISRJB1) PARM(ISRJPA) SCRNAME(BATCH) NOCHECK/ISPF 5/
/SP6/0/SELECT PGM(ISRPTC) SCRNAME(CMD)/ISPF 6/
/SP7/0/SELECT PGM(ISPYXDR) PARM(&ZTAPPLID) SCRNAME(DTEST) NOCHECK/SP7/
/SP8/0/SELECT PANEL(ISRLPRIM) SCRNAME(LMF)/ISPF8/
/SP9/0/SELECT PANEL(ISRDIIS) ADDPOP/ISPF 9/
/SP10/0/SELECT PGM(ISRSCLM) SCRNAME(SCLM) NOCHECK/ISPF 10/
/SP11/0/SELECT PGM(ISRUDA) PARM(ISRWORK) SCRNAME(WORK)/ISPF 11/
/SP31/0/SELECT PGM(ISRUDA) PARM(ISRUDA1) SCRNAME(LIBUTIL)/ISPF 3.1/
/SP32/0/SELECT PGM(ISRUDA) PARM(ISRUDA2) SCRNAME(DSUTIL)/ISPF 3.2/
/SP33/0/SELECT PGM(ISRUMC) SCRNAME(MCOPY)/ISPF 3.3/
/SP34/0/SELECT PGM(ISRUDL) PARM(ISRUDLP) SCRNAME(DSLIST)/ISPF 3.4/
/SP35/0/SELECT PGM(ISRURS) SCRNAME(RESET)/ISPF 3.5/
/SP36/0/SELECT PGM(ISRUHC) SCRNAME(HARDCOPY)/ISPF 3.6/
/SP37/0/SELECT PANEL(ISPUDL) SCRNAME(DOWNLOAD)/ISPF 3.7/
/SP38/0/SELECT PGM(ISRUOLP) SCRNAME(OUTLIST)/ISPF 3.8/
/SP39/0/SELECT PANEL(ISPUCMA) ADDPOP SCRNAME(CMDTABLE)/ISPF 3.9/
/SP311/0/SELECT PGM(ISRFMT) SCRNAME(FORMAT)/ISPF 3.11/
/SP312/0/SELECT PGM(ISRSSM) SCRNAME(SUPERC)/ISPF 3.12/
/SP313/0/SELECT PGM(ISRSEPRM) SCRNAME(SUPERCE) NOCHECK/ISPF 3.13/
/SP314/0/SELECT PGM(ISRSFM) SCRNAME(SRCHFOR)/ISPF 3.14/
/SP315/0/SELECT PGM(ISRSEPRM) PARM(S4) SCRNAME(SRCHFORE) NOCHECK/3.15/
/SCANMEM/0/SELECT CMD(%SCANMEM &ZPARM)/SCAN MEMBER PATTERN FOR PDS/
/U2/0/SELECT CMD(%CE)/CUSTOM EDITOR/
/CE/0/SELECT CMD(%CE)/CUSTOM EDITOR/
/CX/0/SELECT CMD(%CX)/SCREEN EDITOR/
/IMAGEJCL/0/SELECT CMD(%IMAGEJCL)/IMAGECOPY JCL WRITER/
/DISPUTIL/0/SELECT CMD(%DISPUTIL)/DISPLAY RUNNING DB2 UTILITIES/
/DISPPEND/0/SELECT CMD(%DISPPEND)/DISPLAY RESTRICTED DATABASES/
/PAD/0/SELECT CMD(%PAD)/CLIPBOARD/
/$/0/SELECT CMD(%$)/SESSION COST/
/DS/0/SELECT PGM(ISRUDL) PARM(ISRUDLP) SCRNAME(DSLIST)/ISPF 3.4/
/DLE/0/SELECT CMD(%LIBRARN)/ELIPS/
/ISPCMDU/0/SELECT CMD(%ISPCMDU)/USER COMMANDS TABLE/
/SITECMDS/4/SELECT CMD(%SITECMDS)/USER COMMANDS TABLE/
/QST/0/SELECT PGM(ISFISP) NOCHECK NEWAPPL(ISF) PARM(&ZPARM)/SDSF/
*/
