/* Rexx RunSQL */
Trace O
Parse Source . . Me .
Address ISPEXEC
'ISREDIT MACRO (DB2sub) NOPROCESS'
If RC = 20 Then Do
    ZEDSMSG = 'Not a TSO command'
    ZEDLMSG = Me 'should be invoked as an Edit/View macro'
   'SETMSG MSG(ISRZ000)'
    Exit 1
End

PARSE VALUE DB2sub with '-' DB2sub
If strip(DB2sub)= '' Then Do
   ZEDSMSG = 'Syntax : RUNSQL -DB2SSID'
   'SETMSG MSG(ISRZ000)'
   Exit 1
End

'Control Errors Return'
x = OutTrap("DB2out.","*")
    CALL AllocateIn
    CALL AllocateOut
    Call ExecuteDSNTEP2
    Call CleanUp
x = OutTrap("OFF")
Exit 1

AllocateIn :
Do queued();Pull;End
"ISREDIT (first) = linenum .zf"
"ISREDIT (last)  = linenum .zl"
   Do i = first to last by 1
     "ISREDIT (Linebuf) = line" i
     Queue Linebuf
   End
Queue ''

Address TSO
'ALLOC F(SYSIN) REUSE NEW DEL UNIT(SYSDA) LRECL(80) RECFM(F B)
 SPACE(10,10) TRACK RELEASE DSO(PS) BLKSIZE(80)'
'EXECIO' Queued() 'DISKW SYSIN (FINIS'
"Delete '"USERID()".SYSPRINT'"
Do queued();Pull;End

RETURN

AllocateOut :
Address TSO "ALLOC F(SYSPRINT) NEW UNIT(SYSDA) SPACE(8,2)",
            "TRACKS REUSE DSNAME('"USERID()".SYSPRINT')"
Return

ExecuteDSNTEP2:
Address ISPEXEC "LIBDEF STEPLIB DATASET ID("'SYS0.DB2.PROD.DSNLOAD'")"

Call MeasureStart
Address TSO "NEWSTACK"
Queue "RUN PROGRAM(DSNTEP2) PLAN(DSNTEP2) ",
      "LIBRARY('SYS0.DB2.#510.PROD.RUNLIB.LOAD') PARMS('ALIGN(LHS)')"
Queue "END"
Queue
Address TSO "DSN SYSTEM("DB2sub")"
Address TSO "DELSTACK"
Call MeasureEnd

Address ISPEXEC "LMINIT DATAID(DSID) DDNAME(SYSPRINT)"
Address ISPEXEC "BROWSE DATAID("DSID")"
Address ISPEXEC "LMFREE DATAID("DSID")"
Return

CleanUp:
Address TSO "Delete '"USERID()".SYSPRINT'"
RETURN

MeasureStart: Procedure Expose ELA CPU SRV CostB CostE
ELA = TIME('R')
CPU = SYSVAR(SYSCPU)
SRV = SYSVAR(SYSSRV)
Call CostSession
CostB = RESULT
Return

MeasureEnd: Procedure Expose ELA CPU SRV CostB CostE
ELA = TIME('R')      - ELA
CPU = SYSVAR(SYSCPU) - CPU
SRV = SYSVAR(SYSSRV) - SRV
Call CostSession
CostE = RESULT - CostB
Queue '                                         '
Queue '-----------------------------------------'
Queue ' Elapsed Time Wall Clock : 'ELA
Queue ' CPU Seconds             : 'CPU
Queue ' Service Units           : 'SRV
Queue ' Estimated Cost          :$'CostE
Queue '-----------------------------------------'
Queue '                                         '
Address TSO 'EXECIO' 0 'DISKW SYSPRINT (FINIS'
Address TSO "ALLOC F(SYSPRINT) MOD REUSE DSNAME('"USERID()".SYSPRINT')"
Address TSO 'EXECIO' Queued() 'DISKW SYSPRINT (FINIS'
Return

CostSession:
cp1 = sysvar(syscpu)                        /* get cpu time          */
hour = substr(time(),1,2)                   /* get hour of the day   */
If (hour > 16) | (hour < 8)                 /* prime or non-prime?   */
  Then cost = .07                           /* non-prime cost factor */
  Else cost = .13944444                     /* prime cost factor     */
Result = 3.2*cp1*cost
Return Result
