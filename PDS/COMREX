/* REXX COMREX                                                        */
/* .................................................................. */
/* Author  : Kannan AK                                                */
/* COMREX  : To comment REXX                                          */
/* Notice  : None                                                     */
/* Version : Alpha                                                    */
/* .................................................................. */
ADDRESS ISPEXEC
"ISREDIT MACRO (OPT) NOPROCESS"
IF RC = 20 THEN DO
    ZEDSMSG = "NOT A TSO COMMAND"
   "SETMSG MSG(ISRZ000)";EXIT
END
OPT=STRIP(OPT,B);OPT = TRANSLATE(OPT)
IF  (OPT \= 'S' & OPT \= 'O' & OPT \= '?')  THEN
     DO
          ZEDSMSG = "'COMREX ?' FOR HELP"
          "SETMSG MSG(ISRZ001)"
          EXIT
     END
IF OPT = '?' THEN DO
  zerrtp = 'CRITICAL'
  zerrlm   = ,
  left(        "  Comment Edit Macro Help                         ",80),
  left('22'x   "  Utility        : To comment REXX                ",80),
  left('22'x   "  Command Format : COMREX S/O/?                   ",80),
  left('22'x   "  Line Command   : P                              ",80),
  left('22'x   "  S Shifts lines being commented                  ",80),
  left('22'x   "  O Overlays the /* */inplace 1/72                ",80)
  zerrsm   = ""
  zerrhm   = "*"
  zerralrm = "NO"
  "ISPEXEC SETMSG MSG(ISRZ003)"
  EXIT
END
"ISREDIT PROCESS RANGE P"
IF RC > 0 THEN DO
      ZEDSMSG = "IMPROPER LINE COMMAND"
      "SETMSG MSG(ISRZ001)"
       EXIT
END
"ISREDIT (RANG) = RANGE_CMD"
"ISREDIT (FIRST) = LINENUM .ZFRANGE";
"ISREDIT (LAST) = LINENUM .ZLRANGE" ;
DO I = FIRST TO LAST BY 1
     "ISREDIT (LINEBUF) = LINE" I

     IF RANG = 'P' & OPT = 'S' THEN DO
        LINEBUF=INSERT('/*',LINEBUF,0,2)
        LINEBUF=INSERT('*/',LINEBUF,70,2)
     END
     IF RANG = 'P' & OPT = 'O' THEN DO
        LINEBUF=OVERLAY('/*',LINEBUF,1,2)
        LINEBUF=OVERLAY('*/',LINEBUF,71,2)
     END

     "ISREDIT LINE" I " = " '(LINEBUF)'
END
ZEDSMSG = (LAST-FIRST+1) "LINE(S) COMMENTED"
"SETMSG MSG(ISRZ000)"
EXIT
