/* rexx hist - now reflist are there */
arg hlimit
call BarInit
elapsed = sysvar(syscpu)
if hlimit > 0 then maxdatasets = hlimit
else maxdatasets = 100
call tbinit
call tbcreate
address ispexec
"control errors return"
"vget (zlogname) shared"
syslog = zlogname
   hdsn ="'"||syslog||"'"
   avail = sysdsn(hdsn)
   if avail \= "OK" then do
       zedsmsg = avail
      "ispexec setmsg msg(isrz000)";exit
   end
   address tso "alloc ds("hdsn") dd(histdd) shr"
   if rc \= 12 then
      if rc \= 0 then do
       zedsmsg = "file alloc error " rc
       "ispexec setmsg msg(isrz001)" ;exit
      end
hline. = ''
arch.  = ''
address ispexec
"control errors return"
call getlog
hyp = '-';threshold = 0
do i=hline.0 to 1 by -1  until threshold = maxdatasets
   /* say i '-' threshold '-' maxdatasets '-' h_dset */
   if pos('Edit - Save',hline.i)  = 0  then iterate
   if pos('SYS'date('j'),hline.i) > 0  then iterate
   parse upper value hline.i with h_time h_func hyp ,
                     h_fate hyp h_dset hyp h_rest
   if h_func = 'EDIT' & h_fate = 'SAVE' then do
      call dedup
      if dup then nop
      else call hit
   end
   Address Ispexec "Rempop"
   If threshold // 10 = 0 Then Call ShowProgress
end
call popdataset

qui :
   drop hline. junk
   address ispexec "tbend   qlp"
   if zcmd = 'r' then
      address ispexec "select cmd(%hist1)"
   exit 0

hit :
threshold = threshold + 1
arch.threshold = h_dset
ttstr = h_dset
star = ' '
address ispexec "tbmod   qlp order"
return

popdataset :
       elapsed = sysvar(syscpu) - elapsed
       ZEDLMSG = 'Search took' elapsed 'seconds.'
       "ISPEXEC SETMSG MSG(ISRZ000)"
       address ispexec "tbtop   qlp"
       do forever until rc >= 8
          address ispexec "tbdispl qlp panel(histpnl) autosel(no)" ,
                           message cursor csrrow "position(crp)"
          call navigate
       end
return

dedup2 : /* not called - dedup with presence check */
dup = 0
if sysdsn("'"||h_dset||"'") == 'OK' then do
   do k = threshold to 1 by -1 while dup = 0
      if arch.k =  h_dset then dup = 1
   end
end
else dup = 1
return

dedup :
dup = 0
do k = threshold to 1 by -1 while dup = 0
   if arch.k =  h_dset then dup = 1
end
return

editarch :
if ttstr = '' then return
if editmode = 'B' then do
      "ISPEXEC browse dataset('"ttstr"')";star = 'Browsed';end
else
   if editmode = 'V' then do
      "ISPEXEC view dataset('"ttstr"')";star = 'Viewed';end
   else do
      "ISPEXEC edit dataset('"ttstr"')";star = 'Edited';end
if rc > 4 then do
    star = 'Error RC='RC
   "ISPEXEC setmsg msg(isrz002)";
end
return

tbinit :
message = 'msg()'
cursor  = 'cursor()'
csrrow  = 'csrrow(1)'
retcode = 0
return

tbcreate :
    address ispexec
   "control errors return"
   address ispexec "tbopen qlp"                             /* exist? */
   if rc = 8 then do
      nop                                      /* send to recycle bin */
   end
   else do
      address ispexec "tbend    qlp"           /* send to recycle bin */
   end
                                                       /* create anew */
   address ispexec "tbcreate qlp keys(ttstr) names(star)"
return

navigate :
   upper zcmd
   if zcmd = 'CANCEL' | zcmd = 'END' then do
          address ispexec "tbend qlp"
          exit 0
   end
do while ztdsels > 0
   if zcmd = 'CANCEL' | zcmd = 'END' then do
          address ispexec "tbend qlp"
          exit 0
   end

   if (l \= ' ') then do
       editmode = l
       l = ''
       call editarch
       address ispexec "tbmod   qlp order"
       address ispexec "tbdispl qlp panel(histpnl) autosel(no)" ,
                           message cursor csrrow "position(crp)"
   end
   else do
       l = ''
       address ispexec "tbdispl qlp panel(histpnl) autosel(no)" ,
                           message cursor csrrow "position(crp)"
   end
   if rc >= 12 then 'ispexec setmsg msg(isrz002)'
end
if rc = 8 then return
address ispexec "tbtop  qlp"
address ispexec "tbskip qlp number("ztdtop")"
return

getlog :
address ispexec
'lminit  dataid(did) ddname(histdd) enq(shr)'
'lmopen  dataid('did') option(input)'
lmgetrc=rc
j = 0
address ispexec
"control errors return"
do while(lmgetrc=0)
  'lmget dataid('did') mode(invar) dataloc(line) datalen(len)',
    ' maxlen(32760)'
  lmgetrc=rc
  if lmgetrc=0 then
    do
      j = j + 1
      hline.j = line
      If j // 200 = 0 Then Call ShowProgress
    end
end
'lmfree dataid('did')'
address tso 'free f(histdd)'
hline.0 = j
return

BarInit:
ColorCyl= 'ON'
wipmsg  = 'Reading log file...'
BARDYE  = 'TURQ'
barpointer = 0
Address Ispexec
      "Control display lock"
      "Addpop"
      "Display panel(gebarpnl)"
      "Rempop"
wipmsg  = 'Formatting history...'
Return

ShowProgress :
      Address Ispexec
      If barpointer > 50 Then Do
         BARCHAR = ''
         barpointer = 0

         If ColorCyl = 'ON' Then Do
            Select
              When BARDYE  = 'YELLOW' Then BARDYE  = 'RED'
              When BARDYE  = 'RED'    Then BARDYE  = 'BLUE'
              When BARDYE  = 'BLUE'   Then BARDYE  = 'TURQ'
              When BARDYE  = 'TURQ'   Then BARDYE  = 'WHITE'
              When BARDYE  = 'WHITE'  Then BARDYE  = 'PINK'
              When BARDYE  = 'PINK'   Then BARDYE  = 'YELLOW'
              otherwise NOP
            End
         End

      End
      Else Do
         BARCHAR = Copies('k',barpointer)
         barpointer = barpointer + 1

         If ColorCyl = 'ON' Then Do
            Select
              When BARDYE  = 'YELLOW' Then BARDYE  = 'RED'
              When BARDYE  = 'RED'    Then BARDYE  = 'BLUE'
              When BARDYE  = 'BLUE'   Then BARDYE  = 'TURQ'
              When BARDYE  = 'TURQ'   Then BARDYE  = 'WHITE'
              When BARDYE  = 'WHITE'  Then BARDYE  = 'PINK'
              When BARDYE  = 'PINK'   Then BARDYE  = 'YELLOW'
              otherwise NOP
            End
         End

      End
      "Control display lock"
      "Addpop"
      "Display panel(gebarpnl)"
      "Rempop"
Return

