/* Rexx */
/* .................................................................. */
/* Author  : Kannan AK                                                */
/* MASSSTAT: Mass ISPF statistic generator                            */
/* Notice  : Include your datasets at the bottom of the exec          */
/*           Output will be under <userid>.<cue>.MEMBERS              */
/*           Exceptions will not generate output                      */
/* Version : Alpha                                                    */
/* .................................................................. */
dlm = '/'
Call GetInstreamCommands
 Do Queued()
   Pull  c
   Upper c
   If word(c,1) = "/*" Then Iterate
   If c = ""           Then Iterate
   dlm=substr(c,1,1)
   Parse Var c (dlm) dsn (dlm) mid .
   Call GetStat dsn mid
 End
Do queued();Pull;End
Exit 0


GetStat:
Arg LMDsn Cue
If LMDsn = '' Then Do
   Say 'Missing input file name.'
   Return
End
If Cue = ''   Then Do
   Say 'Missing Cue name. USERID.CUE.MEMBERS will be the output file.'
   Return
End

Address ISPEXEC
 RC = 0
 "CONTROL ERRORS RETURN"
 "LMINIT DATAID(STATID)  DATASET('"LMDsn"')"
 LastRC = RC
 If RC = 0 Then Do
   "LMOPEN  DATAID("STATID")"
   "LMMLIST DATAID("STATID") OPTION(SAVE) STATS(YES) GROUP("Cue")"
 End
 Else Do
   "ISPEXEC GETMSG MSG(ISRZ002) SHORTMSG(SMSG)"
   Say 'LMINIT Service has returned 'LastRC
   Say LMDsn ':' SMSG
   Return
 End
 "LMFREE DATAID("STATID")"
Say userid()||'.'Cue'.'||MEMBERS has been created
Return

GetInstreamCommands:
exec_line = 40
Do queued();Pull;End
Do Until substr(line,1,9)='/*COMMAND'
  line = sourceline(exec_line)
  exec_line = exec_line + 1
End
Do until substr(line,1,2)='*/'
  line = sourceline(exec_line)
  If substr(line,1,2) \= '*/' Then
    Queue line
    exec_line = exec_line + 1
End
Return

/*COMMAND
/USERID.HANNA.LOADLIB/STAT1
/USERID.HANNA.CNTL/STAT2
/USERID.HANNA.CNTL.BACKUP/STAT3
*/
