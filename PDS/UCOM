/* REXX UCOM                                                          */
/* .................................................................. */
/* Author  : Kannan AK                                                */
/* UCOM    : To uncomment JCL or COBOL                                */
/* Notice  : None                                                     */
/* Version : Alpha                                                    */
/* .................................................................. */
ADDRESS ISPEXEC
"ISREDIT MACRO (OPT) NOPROCESS"
IF RC = 20 THEN DO
    ZEDSMSG = "NOT A TSO COMMAND"
   "SETMSG MSG(ISRZ000)";EXIT
END
OPT=STRIP(OPT,B);OPT = TRANSLATE(OPT)
IF  (OPT \= 'S' & OPT \= 'O' & OPT \= '?')  THEN
     DO
          ZEDSMSG = "TYPE 'UCOM ?' FOR HELP"
          "SETMSG MSG(ISRZ001)"
          EXIT
     END
IF OPT = '?' THEN DO
  zerrtp = 'CRITICAL'
  zerrlm   = ,
  left(        "  Uncomment Edit Macro Help                       ",80),
  left('22'x   "  Utility        : To uncomment JCL or COBOL      ",80),
  left('22'x   "  Syntax         : UCOM S/O/?                     ",80),
  left('22'x   "  Line Command   : J for JCL/C for COBOL          ",80),
  left('22'x   "  S Shifts lines being commented                  ",80),
  left('22'x   "  O Overlays the * inplace 3/7                    ",80)
  zerrsm   = ""
  zerrhm   = "*"
  zerralrm = "NO"
  "ISPEXEC SETMSG MSG(ISRZ003)"
  EXIT
END
"ISREDIT PROCESS RANGE C J"
IF RC > 0 THEN DO
      ZEDSMSG = "IMPROPER LINE COMMAND"
      "SETMSG MSG(ISRZ001)"
       EXIT
END
"ISREDIT (RANG) = RANGE_CMD"
"ISREDIT (FIRST) = LINENUM .ZFRANGE";
"ISREDIT (LAST) = LINENUM .ZLRANGE" ;
DO I = FIRST TO LAST BY 1
     "ISREDIT (LINEBUF) = LINE" I
     IF RANG = 'C' & OPT = 'S' THEN
        LINEBUF=DELSTR(LINEBUF,7,1)
     IF RANG = 'C' & OPT = 'O' THEN
        LINEBUF=OVERLAY(' ',LINEBUF,7,1)
     IF RANG = 'J' & OPT = 'S' THEN
        LINEBUF=DELSTR(LINEBUF,3,1)
     IF RANG = 'J' & OPT = 'O' THEN
        LINEBUF=OVERLAY(' ',LINEBUF,3,1)
     "ISREDIT LINE" I " = " '(LINEBUF)'
END
ZEDSMSG = (LAST-FIRST+1) "LINE(S) UNCOMMENTED"
"SETMSG MSG(ISRZ000)"
EXIT
