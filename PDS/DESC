/* Rexx                                                               */
/* .................................................................. */
/* Author  : Kannan AK                                                */
/* Desc    : Give struture of the DB2 tables                          */
/* Syntax  : TSO DESC DB2SSID CREATOR.TABLE                           */
/* Version : Alpha                                                    */
/* .................................................................. */
Address ISPEXEC 'Control Errors Return'
PARSE UPPER ARG DB2sub Creator .
PARSE VALUE Creator with Creator '.' Table

If strip(DB2sub)='' | strip(Creator) = '' | strip(Table) = '' Then Do
   Say 'Syntax : TSO DESC DB2SSID CREATOR.TABLE'
   Exit 8
End

x = OutTrap("DB2out.","*")
CALL AllocateIn
CALL AllocateOut
Call ExecuteDSNTEP2
Call CleanUp
x = OutTrap("OFF")
Exit 1

AllocateIn :
Address TSO

Do queued();Pull;End
Queue "SELECT   NAME, COLTYPE, LENGTH, SCALE, NULLS"
Queue "FROM     SYSIBM.SYSCOLUMNS"
Queue "WHERE    TBNAME    = '"Table"'"
Queue "AND      TBCREATOR = '"Creator"'"
Queue "ORDER BY COLNO"
Queue "WITH     UR;"

'ALLOC F(SYSIN) REUSE NEW DEL UNIT(SYSDA) LRECL(80) RECFM(F B)
 SPACE(10,10) TRACK RELEASE DSO(PS) BLKSIZE(80)'
'EXECIO 6 DISKW SYSIN (FINIS'

"delete "USERID()".SYSPRINT"
Do queued();Pull;End
RETURN

AllocateOut :
Address TSO
            "ALLOC F(SYSPRINT) NEW UNIT(SYSDA) SPACE(8,2)",
            "UNIT(sysda)",
            "TRACKS REUSE DSNAME('"USERID()".SYSPRINT')"
Return

ExecuteDSNTEP2:
Address ISPEXEC
"LIBDEF STEPLIB DATASET ID("'SYS0.DB2.PROD.DSNLOAD'")"
Address TSO "NEWSTACK"
QUEUE "RUN PROGRAM(DSNTEP2) PLAN(DSNTEP2) ",
      "LIBRARY('SYS0.DB2.#510.PROD.RUNLIB.LOAD') PARMS('ALIGN(LHS)')"
QUEUE "END"
QUEUE
address TSO "DSN SYSTEM("DB2sub")"
Address TSO "DELSTACK"

ADDRESS ISPEXEC "LMINIT DATAID(DSID) DDNAME(SYSPRINT)"
ADDRESS ISPEXEC "BROWSE DATAID("DSID")"
ADDRESS ISPEXEC "LMFREE DATAID("DSID")"
RETURN

CleanUp:
  address TSO "delete '"USERID()".SYSPRINT'"
RETURN

