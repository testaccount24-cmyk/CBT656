/* REXX - SDSFST : Display the SDSF STATUS data                       */
/* No parm default to your userid                                     */
/* Give userid as parm if you need to look at other ids's jobs        */
/* .................................................................. */
/* Author  : Kannan AK                                                */
/* SDSFST  : Return job status                                        */
/* Notice  : None                                                     */
/* Version : Alpha                                                    */
/* .................................................................. */
Arg User
alpha = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890@#$'
If User = '' Then User = Userid()
k = 1
Address TSO
Do i = 1 To 39
   x = outtrap("var.")
   'ST' User||substr(alpha,i,1)
   x = outtrap(off)
   Do n = 1 To var.trapped
      sto.k = var.n
      k = k + 1
   End
End
sto.0 = k - 1
Queue Date() ':' Time() ':' SDSF STATUS ':' User
Queue copies(' ',255)
Do j = 1 To sto.0
   If pos('NOT FOUND',sto.j) > 0 Then NOP
   Else queue sto.j
End
Address ispexec 'VGET (ZSCREEN) SHARED'
ddname='$TRAP$'zscreen
'ALLOC F('ddname') UNIT(VIO) DSO(PS) RECFM(V B) LRECL(255) NEW DEL REU'
'EXECIO 'queued()' DISKW 'ddname' (FINIS'
Address ispexec 'LMINIT DATAID(TRAP) DDNAME('ddname') ENQ(SHR)'
Address ispexec 'BROWSE DATAID('trap')'
Address ispexec 'LMFREE DATAID('trap')'
'FREE F('ddname')'
Exit
