/* REXX Enable TSO TEST trapping of SELECT CMD(xxx) programs.        */
/* Very release dependent and subject to breakage at any time.       */
/* DO NOT USE AS PART OF A SUPPORTED INTERFACE.  Side effects unknown*/
/* This code is not supported . Doug Nadel                           */
Numeric digits 12
catp = ptr(16+ptr(120+ptr(56+ptr(ptr(24+ptr(112+ptr(132+ptr(540))))))))
found = 0
Do offset = 40 to 50 by 2 Until found = 1
  If storage(d2x(offset+catp),2)='9210'x Then /* MVI INSTR           */
    Do
      /* alter the setrp to be rc=0                                  */
      If '' \= storage(d2x(offset+catp),2,'9200'x) Then
        Do
          Say 'TSO TEST will now catch abends in SELECT CMD() commands'
          found = 1
        End
    End
  Else
    If storage(d2x(46+catp),2)='9200'x Then
      Do
        /* alter the setrp to be rc=16                               */
        If ''\= storage(d2x(46+catp),2,'9210'x) Then
          Do
            Say 'TSO TEST will no longer trap abends',
                'in SELECT CMD() commands'
            found = 1
          End
      End
End
If found=0 Then
  Do
    Say 'SELECT CMD() abend handling was not changed.'
    Say 'Module ISPSUBX must not be in read-only storage.'
    Say 'You can preload ISPSUBX with TSO TEST before starting ISPF.'
    Say 'For example:'
    Say
    Say '    TEST ''SYS1.ISP.SISPLPA(ISRPCP)'' CP'
    Say '    ISPF TEST'
    Say '    LOAD ''SYS1.ISP.SISPLPA(ISPSUBX)'''
    Say '    GO'
    Say
  End
Exit
ptr:
Return c2d(bitand(storage(d2x(Arg(1)),4),'7FFFFFFF'x))

